apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// apply signing configuration if config file exists
if (file('signing.gradle').exists()) {
    apply from: 'signing.gradle'
}

android {
    compileSdkVersion 27
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "me.devsaki.hentoid"
        minSdkVersion 15
        // Hold updating targetSdkVersion until implementing an image viewer
        // or Perfect Viewer implements Content URI
        //noinspection ExpiringTargetSdkVersion
        targetSdkVersion 23
        versionCode 77
        versionName '1.5.10'
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        buildConfigField 'String', 'UPDATE_URL', '"https://raw.githubusercontent.com/avluis/Hentoid/master/app/"'
    }
    buildTypes {
        debug {
            minifyEnabled true
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"

            proguardFile 'proguard-rules-debug.pro'
        }
        release {
            debuggable false
            minifyEnabled true

            proguardFile 'proguard-rules-release.pro'
            testProguardFile 'proguard-rules-test.pro'
            // Library specific proguard files
            proguardFiles fileTree(dir: '../.proguard', include: ['*.pro']).asList().toArray()
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    // Compile any included libraries
    implementation fileTree(include: ['*jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.6'
    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'
    // Stetho core
    implementation 'com.facebook.stetho:stetho:1.5.0'
    // Support libraries
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:preference-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.android.gms:play-services-safetynet:16.0.0' // <-- this one to fix the SSLHandshake error with okhttp on Android 4.1-4.4 when server only supports TLS1.2
    // Image loader: github.com/bumptech/glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0'
    // Intro screens or slides: https://github.com/apl-devs/AppIntro
    //implementation 'com.github.apl-devs:AppIntro:v4.2.2'
    implementation 'com.github.apl-devs:appintro:v4.2.3'
    // Java serialization/deserialization (Java Objects into JSON and back): github.com/google/gson
    implementation 'com.google.code.gson:gson:2.8.5'
    // Timber (Extensible Logger): github.com/JakeWharton/timber
    implementation 'com.jakewharton.timber:timber:4.7.1'
    // Download app updates (without user prompts): github.com/smanikandan14/ThinDownloadManager
    implementation 'com.mani:ThinDownloadManager:1.3.0'
    // HTTP & HTTP/2 client: github.com/square/okhttp
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    // For downloading images
    implementation 'com.android.volley:volley:1.1.1' // <-- if this evolves, please examine RequestQueueManager.getRequestQueue for evolutions
    // EventBus: github.com/greenrobot/EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'
    // JSOUP HTML parser: jsoup.org/download
    implementation 'org.jsoup:jsoup:1.11.3'
    //    https://github.com/aNNiMON/Lightweight-Stream-API
    implementation 'com.annimon:stream:1.2.0'
    // LeakCanary: https://github.com/square/leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'
    // Specific UI layout for tag mosaic : https://github.com/google/flexbox-layout
    implementation 'com.google.android:flexbox:1.0.0' // <- dont upgrade that until the entire app switches to AndroidX
    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation 'commons-io:commons-io:2.6'
    // ScrollView with MaxHeight (tag display in advanced search UI)
    implementation 'com.bskim:maxheightscrollview:1.0.0@aar'

    implementation 'pl.droidsonroids:jspoon:1.3.2'
    implementation 'pl.droidsonroids.retrofit2:converter-jspoon:1.3.2'

    //    https://github.com/square/retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    //    https://github.com/ReactiveX/RxJava/tree/1.x   <== TODO : refactor dirpicker package to rely only on rxjava2
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'io.reactivex:rxandroid:1.2.1'

    //    https://github.com/ReactiveX/RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.16'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    // For ViewModel to work
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:viewmodel:1.1.1"
}

apply plugin: 'com.google.gms.google-services'
