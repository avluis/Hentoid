apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.sonarqube'
//apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'

// apply signing configuration if config file exists
if (file('signing.gradle').exists()) {
    apply from: 'signing.gradle'
}

aboutLibraries {
    exclusionPatterns = [~"com_google_android.*",~"android.*",~"com_mikepenz__fastadapter_extensions.*"]
    configPath = "about-config"
    additionalLicenses {
        LGPL_2_1_or_later
        BSD_3_Clause
    }
}

android {
    compileSdkVersion 30
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "org.nonononoki.hendroid"
        minSdkVersion 21
        //noinspection ExpiringTargetSdkVersion
        targetSdkVersion 30
        versionCode 176
        versionName '1.15.2'

        def fkToken = '\"' + (System.getenv("FK_TOKEN") ?: "") + '\"'
        def includeObjectBoxBrowser = System.getenv("INCLUDE_OBJECTBOX_BROWSER") ?: "false"
        def includeLeakCanary = System.getenv("INCLUDE_LEAK_CANARY") ?: "false"

        buildConfigField "String", "FK_TOKEN", fkToken
        buildConfigField "boolean", "INCLUDE_OBJECTBOX_BROWSER", includeObjectBoxBrowser
        buildConfigField "boolean", "INCLUDE_LEAK_CANARY", includeLeakCanary

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        renderscriptTargetApi 21
        renderscriptSupportModeEnabled false
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        debug {
            minifyEnabled true
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"

            //buildConfigField 'String', 'UPDATE_URL', '"https://raw.githubusercontent.com/avluis/Hentoid/dev/app/"'

            proguardFile 'proguard-rules-debug.pro'
        }
        release {
            debuggable false
            minifyEnabled true

            //buildConfigField 'String', 'UPDATE_URL', '"https://raw.githubusercontent.com/avluis/Hentoid/master/app/"'

            proguardFile 'proguard-rules-release.pro'
            testProguardFile 'proguard-rules-test.pro'

            // Library specific proguard files
            proguardFiles fileTree(dir: '../.proguard', include: ['*.pro']).asList().toArray()
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    def includeObjectBoxBrowser = System.getenv("INCLUDE_OBJECTBOX_BROWSER") ?: "false"
    def includeLeakCanary = System.getenv("INCLUDE_LEAK_CANARY") ?: "false"

    // Compile any included libraries
    implementation fileTree(include: ['*jar'], dir: 'libs')
    implementation fileTree(include: ['*aar'], dir: 'libs')

    //implementation project(':fakkuLib')

    /**
     * TESTING
     */
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.9.0'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'io.kotlintest:kotlintest-assertions:3.4.2'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation 'org.robolectric:robolectric:4.5.1'
    // Should actually use 4.4.1 when it's released to be able to properly init the new Firebase (see https://github.com/robolectric/robolectric/issues/5186#issuecomment-544930108_)
    testImplementation 'net.lachlanmckee:timber-junit-rule:1.0.1'
    // Auto Timber trees that redirect logs to the JUnit output
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'


    /**
     * ANDROID FRAMEWORK
     */
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.5.10"

    // Support libraries
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.4.0-alpha01'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.media:media:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    //implementation 'com.google.android.gms:play-services-safetynet:17.0.0'
    implementation 'androidx.paging:paging-runtime:2.1.2'
    implementation 'androidx.fragment:fragment-ktx:1.3.4'
    implementation "androidx.documentfile:documentfile:1.0.1"
    // <-- if documentfile is bumped, please examine FileExplorer for evolutions
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    def android_work_version = '2.5.0'
    implementation "androidx.work:work-runtime:$android_work_version"
    implementation "androidx.work:work-runtime-ktx:$android_work_version"
    
    // For ViewModel to work
    def android_lifecycle_version = '2.3.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel:$android_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$android_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$android_lifecycle_version"
    kapt("androidx.lifecycle:lifecycle-common-java8:$android_lifecycle_version")


    /**
     * MEDIA
     */
    // Image loader: github.com/bumptech/glide
    def glide_version = "4.12.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt("com.github.bumptech.glide:compiler:$glide_version")
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"

    // Animated PNG (apng) support -> https://github.com/penfeizhou/APNG4Android
    implementation 'com.github.penfeizhou.android.animation:apng:2.8.0'

    /**
     * UI
     */
    // https://github.com/mikepenz/FastAdapter
    def fastadapter_version = '5.4.1'
    implementation "com.mikepenz:fastadapter:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-diff:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-swipe:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-drag:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-expandable:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-scroll:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-paged:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-ui:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-utils:$fastadapter_version"

    // Intro screens or slides: github.com/apl-devs/AppIntro
    implementation 'com.github.AppIntro:AppIntro:6.0.0'

    // Tooltips
    implementation 'com.github.skydoves:balloon:1.3.4'

    // Dropdown lists
    implementation "com.github.skydoves:powerspinner:1.1.7"

    // Popup menus with icons
    implementation 'com.github.skydoves:powermenu:2.1.9'

    // Micro menu
    implementation "com.github.skydoves:submarine:1.0.7"

    // Fast scroller
    implementation "me.zhanghai.android.fastscroll:library:1.1.5"

    // Specific UI layout for tag mosaic : github.com/google/flexbox-layout
    implementation 'com.google.android:flexbox:2.0.1'

    def auto_about_version = '8.9.0'
    implementation "com.mikepenz:aboutlibraries-core:$auto_about_version"
    implementation "com.mikepenz:aboutlibraries:$auto_about_version"


    /**
     * NETWORKING
     */
    // Java serialization/deserialization (Java Objects into JSON and back): https://github.com/square/moshi
    def moshi_version = '1.12.0'
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    // Download app updates (without user prompts): github.com/smanikandan14/ThinDownloadManager
    implementation 'com.mani:ThinDownloadManager:1.3.0'

    // HTTP & HTTP/2 client: github.com/square/okhttp
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    // For downloading images : github.com/google/volley
    implementation 'com.android.volley:volley:1.2.0'
    // <-- if this evolves, please examine RequestQueueManager.getRequestQueue for evolutions

    // Retrofit-ready-ready HTML parser with CSS selectors : https://github.com/DroidsOnRoids/jspoon; uses JSOUP
    def jspoon_version = "1.3.2"
    implementation "pl.droidsonroids:jspoon:$jspoon_version"
    implementation "pl.droidsonroids.retrofit2:converter-jspoon:$jspoon_version"

    // JSOUP HTML parser: github.com/jhy/jsoup
    implementation 'org.jsoup:jsoup:1.13.1'

    //    https://github.com/square/retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    /**
     * UTILS
     */
    //    https://github.com/ReactiveX/RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    // ObjectBox browser dependencies must be set before applying ObjectBox plugin so it does not add objectbox-android
    // (would result in two conflicting versions, e.g. "Duplicate files copied in APK lib/armeabi-v7a/libobjectbox.so").
    def objectbox_version = "2.9.1"
    if (includeObjectBoxBrowser.toBoolean()) {
        debugImplementation "io.objectbox:objectbox-android-objectbrowser:$objectbox_version"
        releaseImplementation "io.objectbox:objectbox-android:$objectbox_version"
    }

    // Timber (Extensible Logger): github.com/JakeWharton/timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // EventBus: github.com/greenrobot/EventBus
    implementation 'org.greenrobot:eventbus:3.2.0'

    // Lightweight Stream : github.com/aNNiMON/Lightweight-Stream-API
    implementation 'com.annimon:stream:1.2.2'

    // LeakCanary: github.com/square/leakcanary
    if (includeLeakCanary.toBoolean()) {
        debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'
    }

    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation 'commons-io:commons-io:2.6'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-text/
    implementation 'org.apache.commons:commons-text:1.9'

    // Cleaner date manipulation
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.0'

    // Archive management
    implementation 'com.github.omicronapps:7-Zip-JBinding-4Android:Release-16.02-2.02'

    // Natural strings comparator
    implementation 'net.grey-panther:natural-comparator:1.1'

    // Efficient hashing
    implementation 'io.whitfin:siphash:2.0.0'

    // Sentence similarity detection
    implementation 'info.debatty:java-string-similarity:2.0.0'


    /**
     * ANALYTICS
     */
    // Import the BoM for the Firebase platform
    //implementation platform('com.google.firebase:firebase-bom:27.1.0')

    // Crashlytics
    //implementation 'com.google.firebase:firebase-crashlytics'
    //implementation 'com.google.firebase:firebase-analytics'


    implementation project(path: ':app:customssiv')
}

sonarqube {
    properties {
        property "sonar.projectKey", "RobbWatershed_hentoid-sonar"
        property "sonar.organization", "robbwatershed-github"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.language", "java"
        property "sonar.sources", "src/main/"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.login", "3d0dc80792bad95f0193e7ed626cf090ac83e208"
    }
}

//apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.objectbox' // apply last