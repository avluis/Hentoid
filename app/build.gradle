apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.sonarqube'
apply plugin: 'com.google.firebase.crashlytics'

// apply signing configuration if config file exists
if (file('signing.gradle').exists()) {
    apply from: 'signing.gradle'
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                url 'https://dl.bintray.com/omicronapps/7-Zip-JBinding-4Android'
            }
        }
        filter {
            includeGroup "net.sf.sevenzipjbinding"
        }
    }
}

android {
    compileSdkVersion 30
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "me.devsaki.hentoid"
        minSdkVersion 21
        //noinspection ExpiringTargetSdkVersion
        targetSdkVersion 30
        versionCode 130 // is updated automatically by BitRise; only used when building locally
        versionName '1.13.9'

        def fkToken = '\"' + (System.getenv("FK_TOKEN") ?: "") + '\"'
        def includeObjectBoxBrowser = System.getenv("INCLUDE_OBJECTBOX_BROWSER") ?: "false"

        buildConfigField "String", "FK_TOKEN", fkToken
        buildConfigField "boolean", "INCLUDE_OBJECTBOX_BROWSER", includeObjectBoxBrowser

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        renderscriptTargetApi 21
        renderscriptSupportModeEnabled false
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        debug {
            minifyEnabled true
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"

            buildConfigField 'String', 'UPDATE_URL', '"https://raw.githubusercontent.com/avluis/Hentoid/dev/app/"'

            proguardFile 'proguard-rules-debug.pro'
        }
        release {
            debuggable false
            minifyEnabled true

            buildConfigField 'String', 'UPDATE_URL', '"https://raw.githubusercontent.com/avluis/Hentoid/master/app/"'

            proguardFile 'proguard-rules-release.pro'
            testProguardFile 'proguard-rules-test.pro'

            // Library specific proguard files
            proguardFiles fileTree(dir: '../.proguard', include: ['*.pro']).asList().toArray()
        }
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    def includeObjectBoxBrowser = System.getenv("INCLUDE_OBJECTBOX_BROWSER") ?: "false"

    // Compile any included libraries
    implementation fileTree(include: ['*jar'], dir: 'libs')
    implementation fileTree(include: ['*aar'], dir: 'libs')

    implementation project(':fakkuLib')

    /**
     * TESTING
     */
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-core:3.6.28'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'io.kotlintest:kotlintest-assertions:3.4.2'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation 'org.robolectric:robolectric:4.5-alpha-3'
    // Should actually use 4.4.1 when it's released to be able to properly init the new Firebase (see https://github.com/robolectric/robolectric/issues/5186#issuecomment-544930108_)
    testImplementation 'net.lachlanmckee:timber-junit-rule:1.0.1'
    // Auto Timber trees that redirect logs to the JUnit output
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'


    /**
     * ANDROID FRAMEWORK
     */
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.20"

    // Support libraries
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha08'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.media:media:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.google.android.gms:play-services-safetynet:17.0.0'
    implementation 'androidx.paging:paging-runtime:2.1.2'
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation "androidx.documentfile:documentfile:1.0.1"
    // <-- if this evolves, please examine FileUtil.fromTreeUriOptimized for evolutions
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.work:work-runtime:2.5.0"

    // For ViewModel to work
    def android_lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$android_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$android_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$android_lifecycle_version"
    kapt("androidx.lifecycle:lifecycle-common-java8:$android_lifecycle_version")


    /**
     * MEDIA
     */
    // Image loader: github.com/bumptech/glide
    def glide_version = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt("com.github.bumptech.glide:compiler:$glide_version")
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"

    // Animated PNG (apng) support -> https://github.com/penfeizhou/APNG4Android
    implementation 'com.github.penfeizhou.android.animation:apng:2.4.2'

    /**
     * UI
     */
    // https://github.com/mikepenz/FastAdapter
    def fastadapter_version = "5.3.2"
    implementation "com.mikepenz:fastadapter:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-diff:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-swipe:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-drag:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-expandable:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-scroll:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-paged:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-ui:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions-utils:$fastadapter_version"

    // Intro screens or slides: github.com/apl-devs/AppIntro
    implementation 'com.github.AppIntro:AppIntro:6.0.0'

    // Tooltips
    implementation 'com.github.skydoves:balloon:1.2.5'

    // Dropdown lists
    implementation "com.github.skydoves:powerspinner:1.1.7"

    // Popup menus with icons
    implementation 'com.github.skydoves:powermenu:2.1.8'

    // Fast scroller
    implementation "me.zhanghai.android.fastscroll:library:1.1.4"

    // Specific UI layout for tag mosaic : github.com/google/flexbox-layout
    implementation 'com.google.android:flexbox:2.0.0'

    /**
     * NETWORKING
     */
    // Java serialization/deserialization (Java Objects into JSON and back): https://github.com/square/moshi
    def moshi_version = '1.11.0'
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    // Download app updates (without user prompts): github.com/smanikandan14/ThinDownloadManager
    implementation 'com.mani:ThinDownloadManager:1.3.0'

    // HTTP & HTTP/2 client: github.com/square/okhttp
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    // For downloading images : github.com/google/volley
    implementation 'com.android.volley:volley:1.1.1'
    // <-- if this evolves, please examine RequestQueueManager.getRequestQueue for evolutions

    // Retrofit-ready-ready HTML parser with CSS selectors : https://github.com/DroidsOnRoids/jspoon; uses JSOUP
    def jspoon_version = "1.3.2"
    implementation "pl.droidsonroids:jspoon:$jspoon_version"
    implementation "pl.droidsonroids.retrofit2:converter-jspoon:$jspoon_version"

    // JSOUP HTML parser: github.com/jhy/jsoup
    implementation 'org.jsoup:jsoup:1.13.1'

    //    https://github.com/square/retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    /**
     * UTILS
     */
    //    https://github.com/ReactiveX/RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // ObjectBox browser dependencies must be set before applying ObjectBox plugin so it does not add objectbox-android
    // (would result in two conflicting versions, e.g. "Duplicate files copied in APK lib/armeabi-v7a/libobjectbox.so").
    def objectbox_version = "2.8.1"
    if (includeObjectBoxBrowser.toBoolean()) {
        debugImplementation "io.objectbox:objectbox-android-objectbrowser:$objectbox_version"
        releaseImplementation "io.objectbox:objectbox-android:$objectbox_version"
    }

    // Timber (Extensible Logger): github.com/JakeWharton/timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // EventBus: github.com/greenrobot/EventBus
    implementation 'org.greenrobot:eventbus:3.2.0'

    // Lightweight Stream : github.com/aNNiMON/Lightweight-Stream-API
    implementation 'com.annimon:stream:1.2.1'

    // LeakCanary: github.com/square/leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'

    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation 'commons-io:commons-io:2.6'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.11'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-text/
    implementation 'org.apache.commons:commons-text:1.4'
    // !!! Don't update that one or Lint will go mad

    // Cleaner date manipulation
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'

    // Archive management
    implementation 'net.sf.sevenzipjbinding:sevenzipjbinding:16.02-2.01'

    // Natural strings comparator
    implementation 'net.grey-panther:natural-comparator:1.1'


    /**
     * ANALYTICS
     */
    // Firebase
    implementation 'com.google.firebase:firebase-analytics:18.0.2'

    // Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics:17.3.1'


    implementation project(path: ':app:customssiv')
}

sonarqube {
    properties {
        property "sonar.projectKey", "RobbWatershed_hentoid-sonar"
        property "sonar.organization", "robbwatershed-github"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.language", "java"
        property "sonar.sources", "src/main/"
        property "sonar.login", "3d0dc80792bad95f0193e7ed626cf090ac83e208"
    }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.objectbox' // apply last