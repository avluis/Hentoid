apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// apply signing configuration if config file exists
if (file('signing.gradle').exists()) {
    apply from: 'signing.gradle'
}

android {
    compileSdkVersion 27
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions { // To appease jenkins builds
        abortOnError false
    }
    defaultConfig {
        applicationId "me.devsaki.hentoid"
        minSdkVersion 15
        // Hold updating targetSdkVersion until implementing an image viewer
        // or Perfect Viewer implements Content URI
        //noinspection OldTargetApi
        targetSdkVersion 23
        versionCode 63
        versionName '1.3.10'
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        buildConfigField 'String', 'UPDATE_URL', '"https://raw.githubusercontent.com/avluis/Hentoid/master/app/update.json"'
    }
    buildTypes {
        debug {
            minifyEnabled true
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"

            proguardFile 'proguard-rules-debug.pro'
        }
        release {
            debuggable false
            minifyEnabled true

            proguardFile 'proguard-rules-release.pro'
            testProguardFile 'proguard-rules-test.pro'
            // Library specific proguard files
            proguardFiles fileTree(dir: '../.proguard', include: ['*.pro']).asList().toArray()
        }
    }
}

dependencies {
    // Compile any included libraries
    implementation fileTree(include: ['*jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.1'
    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'
    // Stetho core
    implementation 'com.facebook.stetho:stetho:1.5.0'
    // Support libraries
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:preference-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    // Image loader: github.com/bumptech/glide
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.4.0'
    // Intro screens or slides: https://github.com/apl-devs/AppIntro
    //implementation 'com.github.apl-devs:AppIntro:v4.2.2'
    implementation 'com.github.apl-devs:appintro:v4.2.3'
    // Java serialization/deserialization (Java Objects into JSON and back): github.com/google/gson
    implementation 'com.google.code.gson:gson:2.8.2'
    // Timber (Extensible Logger): github.com/JakeWharton/timber
    implementation 'com.jakewharton.timber:timber:4.7.0'
    // Download app updates (without user prompts): github.com/smanikandan14/ThinDownloadManager
    implementation 'com.mani:ThinDownloadManager:1.3.0'
    // HTTP & HTTP/2 client: github.com/square/okhttp
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    // For downloading images
    implementation 'com.android.volley:volley:1.1.0'
    // EventBus: github.com/greenrobot/EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'
    // JSOUP HTML parser: jsoup.org/download
    implementation 'org.jsoup:jsoup:1.10.3'
    // RxAndroid (RxJava bindings for Android): github.com/ReactiveX/RxAndroid
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.4'
    //    https://github.com/aNNiMON/Lightweight-Stream-API
    implementation 'com.annimon:stream:1.2.0'
    // LeakCanary: https://github.com/square/leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    //    https://github.com/ApmeM/android-flowlayout
    implementation 'org.apmem.tools:layouts:1.10@aar'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation 'commons-io:commons-io:2.6'
}

apply plugin: 'com.google.gms.google-services'